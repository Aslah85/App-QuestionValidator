#!/usr/bin/perl -w
# question-validator --- Checks if a submitted question is valid.
# Author: Jean-Christophe Petkovich <jcpetkovich@gmail.com>
# Created: 05 Jan 2013
# Version: 0.01

use v5.12;
use warnings;
use strict;
use Getopt::Long qw( :config auto_help );
use IO::File;
use App::QuestionValidator;
use autodie;

GetOptions();

my $fh = new IO::File();

if ( @ARGV == 0 ) {

    $fh->fdopen( fileno(STDIN), 'r' );

    my $questions = 0;
    my $status    = 1;
    my $fields;
    my $lastrow;
    while ( $fields = load_question($fh) ) {

        # If we caught the start of another question at the end of the
        # previous one add the header to the question's fields
        if ($lastrow) {
            unshift @$fields, $lastrow;
            undef $lastrow;
        }

        # If we caught the start of another question at the end of
        # this question, store it for the next iteration
        if ( $fields->[$#$fields][$App::QuestionValidator::ROW_TAG] =~
            /newquestion/i )
        {
            $lastrow = pop @$fields;
        }

        $questions++;
        $status &&= validate($fields);
    }

    unless ($status) {
        say STDERR $questions > 1 ? "All questions " : "Question ",
          "did not pass validation.";
        exit 1;
    }
    else {
        say " ok";
    }

}
else {
    my $status = 1;

    for (@ARGV) {
        if ( -r $_ ) {
            $fh->open( $_, 'r' );

            $App::QuestionValidator::FILENAME = $_;
            print "Opening file $_ for scrutiny:";
            my $fields;
            while ( $fields = load_question($fh) ) {
                $status &&= validate($fields);
            }
        }
    }
    unless ($status) {
        say STDERR @ARGV > 1 ? "All questions " : "Question ",
          "did not pass validation.";
        exit 1;
    }
    else {
        say " ok";
    }
}

__END__

=head1 NAME

question-validator - Validate learn multiple choice questions.

=head1 SYNOPSIS

B<question-validator> [options] [files]

      -h --help      Print this help documentation.

=head1 DESCRIPTION

B<question-validator> takes a list of files to validate one by one, or
can have a question csv piped to it from the commandline.

Keep running the validator on the question and fix the first error
that appears until B<question-validator> returns no errors.

=head1 AUTHOR

Jean-Christophe Petkovich, E<lt>jcpetkovich@gmail.comE<gt>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2013 by Jean-Christophe Petkovich

This program is free software; you can redistribute it and/or modify
it under the same terms as Perl itself, either Perl version 5.8.2 or,
at your option, any later version of Perl 5 you may have available.

=head1 BUGS

None reported... yet.

=cut
